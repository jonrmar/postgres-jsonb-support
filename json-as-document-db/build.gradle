apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

dependencies {
    compile group: 'postgresql', name: 'postgresql', version: '9.0-801.jdbc4'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
}

test {
    testLogging.showStandardStreams = true
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'io.github.jonrmar'
            artifactId 'postgres-as-document-db'
            version '1.0.2'

            from components.java
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Let postgres to work as a document database'
            name 'Postgres as Document Database'
            url 'https://github.com/jonrmar/postgres-jsonb-support'
            organization {
                name 'io.github.jonrmar'
                url 'https://github.com/jonrmar'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/jonrmar/postgres-jsonb-support/issues'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'https://github.com/jonrmar/postgres-jsonb-support/blob/master/LICENSE'
                }
            }
            scm {
                url 'https://github.com/jonrmar/postgres-jsonb-support'
                connection 'scm:git:git://github.com/jonrmar/postgres-jsonb-support.git'
                developerConnection 'scm:git:ssh://github.com/jonrmar/postgres-jsonb-support.git'
            }
            developers {
                developer {
                    name 'Jonathan'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}